plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.5'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'org.openapi.generator' version '7.2.0'
}

group = 'com.techub'
version = '1.0.0'

java {
    sourceCompatibility = '21'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.postgresql:postgresql'
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.flywaydb:flyway-database-postgresql'
    implementation 'io.micrometer:micrometer-registry-prometheus'
    implementation 'com.bucket4j:bucket4j-core:8.10.1'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.3.0'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'
    implementation 'io.swagger.core.v3:swagger-annotations:2.2.20'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.rest-assured:rest-assured'
    testImplementation 'com.tngtech.archunit:archunit-junit5:1.2.1'
    testImplementation 'com.h2database:h2'
}

tasks.named('test') {
    useJUnitPlatform()
}

openApiGenerate {
    generatorName = 'spring'
    inputSpec = "$projectDir/src/main/resources/openapi.yaml".toString()
    outputDir = "${layout.buildDirectory.get().asFile}/generated".toString()
    apiPackage = 'com.techub.rps.boundary.incoming.api'
    modelPackage = 'com.techub.rps.boundary.incoming.dto'
    configOptions = [
            dateLibrary: 'java8',
            interfaceOnly: 'true',
            useSpringBoot3: 'true',
            useTags: 'true',
            skipDefaultInterface: 'true',
            documentationProvider: 'none',
            useJakartaEe: 'true'
    ]
}
sourceSets {
    main {
        java {
            srcDir "${layout.buildDirectory.get().asFile}/generated/src/main/java"
        }
    }
}

tasks.withType(JavaCompile) {
    dependsOn tasks.openApiGenerate
}
